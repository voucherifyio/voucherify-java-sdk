group = 'io.voucherify.client'
version = '10.0.0'

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceSets {
    main {
        java {
            exclude 'io/voucherify/example/**'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId group
            artifactId 'voucherify-java-sdk'
            version version
            from components.java

            pom {
                name = System.getenv('POM_NAME') ?: POM_NAME
                description = System.getenv('POM_DESCRIPTION') ?: POM_DESCRIPTION
                url = System.getenv('POM_URL') ?: POM_URL
                licenses {
                    license {
                        name = System.getenv('POM_LICENCE_NAME') ?: POM_LICENCE_NAME
                        url = System.getenv('POM_LICENCE_URL') ?: POM_LICENCE_URL
                        distribution = System.getenv('POM_LICENCE_DIST') ?: POM_LICENCE_DIST
                    }
                }
                developers {
                    developer {
                        id = System.getenv('POM_DEVELOPER_ID') ?: POM_DEVELOPER_ID
                        name = System.getenv('POM_DEVELOPER_NAME') ?: POM_DEVELOPER_NAME
                    }
                }
                scm {
                    connection = System.getenv('POM_SCM_CONNECTION') ?: POM_SCM_CONNECTION
                    developerConnection = System.getenv('POM_SCM_DEV_CONNECTION') ?: POM_SCM_DEV_CONNECTION
                    url = System.getenv('POM_SCM_URL') ?: POM_SCM_URL
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = System.getenv('RELEASE_REPOSITORY_URL') ?: RELEASE_REPOSITORY_URL
            def snapshotsRepoUrl = System.getenv('SNAPSHOT_REPOSITORY_URL') ?: SNAPSHOT_REPOSITORY_URL
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = System.getenv('NEXUS_USERNAME') ?: NEXUS_USERNAME
                password = System.getenv('NEXUS_PASSWORD') ?: NEXUS_PASSWORD
            }
        }
    }
}

if (project.hasProperty('sign')) {
    signing {
        sign publishing.publications.maven
    }
}

dependencies {
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.6.1'
    compile group: 'com.squareup.retrofit2', name: 'converter-jackson', version: '2.6.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '4.2.0'
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.2'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.0.0'
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.2.0'
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.0.1'
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
}
